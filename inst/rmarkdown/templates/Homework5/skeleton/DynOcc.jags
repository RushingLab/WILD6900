
model{
    # Priors
    alpha0 ~ dnorm(0, 0.1)
    alpha1 ~ dnorm(0, 0.1)

    beta0 ~ dnorm(0, 0.1)
    beta1 ~ dnorm(0, 0.1)

    delta0 ~ dnorm(0, 0.1)
    delta1 ~ dnorm(0, 0.1)

    mu.lp ~ dnorm(0, 0.1)
    sd.p ~ dunif(0, 10)
    tau.p <- 1/(sd.p * sd.p)

    # Likelihood
    for(i in 1:N){
     z[i,1] ~ dbern(psi[i])
     logit(psi[i]) <- alpha0 + alpha1 * X1[i]

     for(k in 1:K){
       logit(p[i, 1, k]) <- lp[i, 1, k] 
       lp[i, 1, k] ~ dnorm(mu.lp, tau.p)
       y[i,1,k] ~ dbern(z[i,1] * p[i, 1, k])
     }

     logit(eps[i]) <- beta0 + beta1 * X2[i]
     logit(gamma[i]) <- delta0 + delta1 * X1[i]

     for(t in 2:nYears){
       z[i, t] ~ dbern(z[i, t - 1] * (1 - eps[i]) + (1 - z[i, t-1]) * gamma[i])

        for(k in 1:K){
          logit(p[i, t, k]) <- lp[i, t, k] 
          lp[i, t, k] ~ dnorm(mu.lp, tau.p)
          y[i,t,k] ~ dbern(z[i,t] * p[i, t, k])
        }
     }
    }
}
    
