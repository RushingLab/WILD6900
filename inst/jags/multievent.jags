
    model{
    # Priors
    phiU ~ dunif(0, 1)
    phiI ~ dunif(0, 1)
    r ~ dunif(0, 1)
    
    p ~ dunif(0, 1) 
    c ~ dunif(0, 1)
    # fn ~ dbeta(1,1)#17.9, 161)
    # fp ~ dbeta(1,1)#62, 186)
    
    # Define state-transition and observation matrices
    # Define probabilities of state S(t+1) given S(t)
    ps[1,1] <- phiU * (1-r)
    ps[1,2] <- phiU * r
    ps[1,3] <- 1-phiU
    ps[2,1] <- 0
    ps[2,2] <- phiI
    ps[2,3] <- 1-phiI
    ps[3,1] <- 0
    ps[3,2] <- 0
    ps[3,3] <- 1
    
    # Define probabilities of O(t) given S(t)
    po[1,1] <- p * c 
    po[1,2] <- 0
    po[1,3] <- p * (1 - c)
    po[1,4] <- 1 - p
    po[2,1] <- 0
    po[2,2] <- p * c
    po[2,3] <- p * (1 - c)
    po[2,4] <- 1 - p
    po[3,1] <- 0
    po[3,2] <- 0
    po[3,3] <- 0
    po[3,4] <- 1
    
    # Likelihood
    for (i in 1:nind){
    # Define latent state at first capture
    z[i,f[i]] <- y[i,f[i]]
    for (t in (f[i]+1):n.occasions){
    # State process: draw S(t) given S(t-1)
    z[i,t] ~ dcat(ps[z[i,t-1],])
    # Observation process: draw O(t) given S(t)
    y[i,t] ~ dcat(po[z[i,t],])
    } #t
    } #i
    }
    
