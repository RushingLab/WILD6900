
model{
    # Priors
    lambda ~ dgamma(0.25, 0.25)
    beta0 ~ dnorm(0, 0.1)
    beta1 ~ dnorm(0, 0.1)

    # Likelihood
    for(j in 1:J){
      N[j] ~ dpois(lambda)
  
      for(k in 1:K){
        y[j, k] ~ dbinom(p[j, k], N[j])
        logit(p[j, k]) <- beta0 + beta1 * hours[j,k]
    
        exp[j, k] <- N[j] * p[j, k]
        E[j, k] <- pow((y[j, k] - exp[j, k]), 2) / (exp[j, k] + 0.5)
    
        y.new[j, k] ~ dbinom(p[j, k], N[j])
        E.new[j, k] <- pow((y.new[j, k] - exp[j, k]), 2) / (exp[j, k] + 0.5)
      } # end k loop
    } # end j loop

    # Derived variables
    fit <- sum(E[,])
    fit.new <- sum(E.new[,])
}
    
